---
title: "Lisp compiler in F#: Expression trees and .NET methods"
date: "2009/06/01 22:36:34"
updated: "2009/06/01 22:36:34"
categories: Compiler
tags: F#,Lisp
permalink: /blog/2009/06/lisp-compiler-in-f-expression-trees-and-net-methods.html
---
<p>This is part 3 of a series of posts on my Lisp compiler written in F#. Previous entries: <a href="/blog/2009/05/lisp-compiler-in-f-introduction.html">Introduction</a>, <a href="/blog/2009/05/lisp-compiler-in-f-parsing-with-fslex-and-fsyacc.html">Parsing with fslex and fsyacc</a> | <a href="http://github.com/timrobinson/fsharp-lisp/tree/master">Browse the full source of the compiler on GitHub</a></p>
<p>Thanks to <em>fslex</em>, <em>fsyacc</em> and the <code>LispVal</code> type, we've ended up with an expression tree that represents our program. To summarise, we've turned this:</p>
<pre class="brush: plain">
(define (fact n) (if (= n 0) 1 (* n (fact (- n 1)))))
(Console.WriteLine "6! = {0}" (fact 6))
(Console.WriteLine "What is your name?")
(Console.WriteLine "Hello, {0}" (Console.ReadLine))
</pre>
<p>...into an F# data structure that looks like this:</p>
<pre class="brush: fsharp">
[List
   [Atom "define"; List [Atom "fact"; Atom "n"];
    List
      [Atom "if"; List [Atom "="; Atom "n"; Number 0]; Number 1;
       List
         [Atom "*"; Atom "n";
          List [Atom "fact"; List [Atom "-"; Atom "n"; Number 1]]]]];
 List
   [Atom "Console.WriteLine"; String "6! = {0}"; List [Atom "fact"; Number 6]];
 List [Atom "Console.WriteLine"; String "What is your name?"];
 List
   [Atom "Console.WriteLine"; String "Hello, {0}";
    List [Atom "Console.ReadLine"]]]
</pre>
<p>We'd like to turn this data structure into actual IL, which we can execute. I'll assume some restrictions:</p>
<ul>
  <li>We're going to compile, not interpret, and we're going to target .NET IL, not x86 machine code, <a href="http://llvm.org/">LLVM</a>, or anything else at this point</li>

  <li>Basic arithmetic (+, -, *, /) on integers<br />
  <code>(- n 1)</code></li>

  <li>Equality comparisons<br />
  <code>(= n 0)</code></li>

  <li><code>if</code> statements<br />
  <code>(if (= n 0) a b)</code></li>

  <li>Call static .NET methods (no <code>new</code> operator and no instance methods)<br />
  <code>(Console.WriteLine "What is your name?")</code></li>

  <li>Define and call our own functions<br />
  <code>(define (fact n) (if (= n 0) 1 (* n (fact (- n 1)))))</code></li>
</ul>
<p>What we'll do is:</p>
<ol>
  <li>Preprocess built-in forms such as arithmetic, <code>define</code>, <code>if</code> and <code>lambda</code> into specific nodes in the expression tree</li>

  <li>Construct an instance of <code>System.Reflection.Emit.ILGenerator</code>. We could write an EXE or DLL file, but for experimentation, it's handy to target a <code>DynamicMethod</code></li>

  <li>Emit IL opcodes that implement the expression tree</li>
</ol>
<p>First, a couple of pattern matching functions to recognise the built-in forms. Strictly speaking, we could write a code generator which recognises the built-in forms directly, but turning them into first-class expression tree nodes early on will hopefully make it easier to apply compiler optimisations, which I hope to add at some point.</p>
<p class="alt"><a href="http://github.com/timrobinson/fsharp-lisp/blob/b13a8c59066040b7c007b0a4bea28a7fc2163fc1/Core/CodeGenerator.fs">CodeGenerator.fs</a></p>
<pre class="brush: fsharp">
// Turn a LispVal into a function or variable name
let extractAtom = function
    | Atom a -&gt; a
    | v -&gt; raise &lt;| Compiler(sprintf "expected atom, got %A" v)

// Note: insertPrimitives accepts a LispVal and returns a LispVal.
// The function keyword combines function declaration with pattern matching.
let rec insertPrimitives = 
    function
    // Convert arithmetic operators into ListPrimitive
    | List (Atom "+" :: args) -&gt; ListPrimitive (Add, args |&gt; List.map insertPrimitives)
    | List (Atom "-" :: args) -&gt; ListPrimitive (Subtract, args |&gt; List.map insertPrimitives)
    | List (Atom "*" :: args) -&gt; ListPrimitive (Multiply, args |&gt; List.map insertPrimitives)
    | List (Atom "/" :: args) -&gt; ListPrimitive (Divide, args |&gt; List.map insertPrimitives)
    | List (Atom "=" :: args) -&gt; ListPrimitive (Equal, args |&gt; List.map insertPrimitives)

    | List (Atom "define" :: args) -&gt;
        match args with
        | [ Atom name; v ] -&gt; 
            // Convert (define variableName value) into VariableDef
            VariableDef (name, insertPrimitives v)

        | [ List (Atom name :: names); body ] -&gt; 
            // Convert (define functionName (x y z) value) into a VariableDef wrapping a LambdaDef
            // This represents a named static .NET method
            VariableDef (name, LambdaDef (names |&gt; List.map extractAtom, insertPrimitives body))

        | _ -&gt; 
            // Note: "raise &lt;| Compiler message" is equivalent to C# "throw new CompilerException(message)"
            raise &lt;| Compiler "expected define name value"

    | List (Atom "if" :: args) -&gt;
        match args with
        | [ testValue; thenValue; elseValue ] -&gt; 
            // Convert (if test then else) into IfPrimitive)
            IfPrimitive (insertPrimitives testValue, insertPrimitives thenValue, insertPrimitives elseValue)

        | _ -&gt; 
            raise &lt;| Compiler "expected three items for if"

    | List (Atom "lambda" :: args) -&gt;
        match args with
        | [ List names; body ] -&gt; 
            // Convert (lambda (x y z) value) into LambdaDef, without a VariableDef
            LambdaDef (names |&gt; List.map extractAtom, insertPrimitives body)

        | _ -&gt; 
            raise &lt;| Compiler "expected lambda names body"

    | List l -&gt; 
        // Apply insertPrimitives recursively on any function invokations
        l |&gt; List.map insertPrimitives |&gt; List

    | v -&gt; v
</pre>
<p>The <code>insertPrimitives</code> function turns our parsed expression tree into this:</p>
<pre class="brush: fsharp">
[VariableDef
   ("fact",
    LambdaDef
      (["n"],
       IfPrimitive
         (ListPrimitive (Equal,[Atom "n"; Number 0]),Number 1,
          ListPrimitive
            (Multiply,
             [Atom "n";
              List [Atom "fact"; ListPrimitive (Subtract,[Atom "n"; Number 1])]]))));
 List
   [Atom "Console.WriteLine"; String "6! = {0}"; List [Atom "fact"; Number 6]];
 List [Atom "Console.WriteLine"; String "What is your name?"];
 List
   [Atom "Console.WriteLine"; String "Hello, {0}";
    List [Atom "Console.ReadLine"]]]
</pre>
<p>We're going to write an F# function that emits IL for one line in our program, and looks like this:</p>
<pre class="brush: fsharp">
let rec compile
    (generator : ILGenerator)
    (defineMethod : string -&gt; Type -&gt; Type list -&gt; #MethodInfo * ILGenerator)
    (env : Map&lt;string, LispVal&gt;)
    (value : LispVal) 
    : (Map&lt;string, LispVal&gt;)
</pre>
<p>What this function signature tells us is:</p>
<ul>
  <li>We have a recursive function called <code>compile</code> (by default, F# functions aren't allowed to call themselves, hence the <code>rec</code> keyword)</li>

  <li>It takes the following parameters:

    <ol>
      <li>An <code>ILGenerator</code>, i.e. the target of the IL we're going to generate</li>

      <li>A function that accepts a <code>string</code>, a <code>Type</code>, a <code>list</code> of <code>Type</code>, and returns a tuple containing a <code>MethodInfo</code> (or a type derived from <code>MethodInfo</code>, hence the #) and another <code>ILGenerator</code>. This will be the callback that <code>compile</code> will call to create a new static method for <code>lambda</code>: the <code>string</code> is a function name, the <code>Type</code> is a return type, and the <code>Type list</code> is a list of parameter types.</li>

      <li>A <code>Map</code> of <code>string</code> to <code>LispVal</code>, i.e. the variables and functions defined by prior statements in the program</li>

      <li>A <code>LispVal</code> representing the statement to generate code for</li>
    </ol>
  </li>

  <li>It returns <code>Map&lt;string, LispVal&gt;</code>, i.e. a copy of <code>env</code>, possibly with some new variables or functions added</li>
</ul>
<p>I'll cover the details of the <code>compile</code> function itself in the next post. In this one I'd like to explain a couple of helper functions:</p>
<ul>
  <li><code>typeOf</code>, which returns the .NET <code>Type</code> denoted by a <code>LispVal</code></li>

  <li><code>lambdaIdent</code>, which retrieves a <code>LambdaDef</code></li>
</ul>
<p>We're using <code>LambdaDef</code> nodes not only to define our own functions (like <code>fact</code> in our example above, which calculates factorials), but also any .NET methods we call. <code>typeOf</code> and <code>lambdaIdent</code> call each other, so we have to define them together with F#'s <code>and</code> keyword in between them:</p>
<ul>
  <li><code>typeOf</code> needs to call <code>lambdaIdent</code> in order to determine the type returned by a function invocation</li>

  <li><code>lambdaIdent</code> needs to call <code>typeOf</code> when it looks at the types of function arguments when deciding which overload of a .NET method to call</li>
</ul>
<pre class="brush: fsharp">
let rec typeOf (env : Map&lt;string, LispVal&gt;) = function
    | ArgRef _ -&gt; typeof&lt;int&gt;
    | Atom a -&gt; a |&gt; ident env |&gt; typeOf env
    | Bool _ -&gt; typeof&lt;bool&gt;
    | IfPrimitive (_, thenValue, elseValue) -&gt;
        match typeOf env thenValue with
        | t when t = typeOf env elseValue -&gt; t
        | _ -&gt; raise &lt;| Compiler("expected 'then' and 'else' branches to have same type")

    | LambdaDef (_, body) -&gt; typeOf env body
    | LambdaRef (methodBuilder, _, _) -&gt; methodBuilder.ReturnType
    | List (Atom a :: args) -&gt; a |&gt; lambdaIdent args env |&gt; typeOf env
    | List (fn :: _) -&gt; raise &lt;| Compiler(sprintf "can't invoke %A" fn)
    | List [ ] -&gt; raise &lt;| Compiler("can't compile empty list")
    | ListPrimitive _ -&gt; typeof&lt;int&gt;
    | Number _ -&gt; typeof&lt;int&gt;
    | String _ -&gt; typeof&lt;string&gt;
    | VariableDef _ -&gt; typeof&lt;Void&gt;
    | VariableRef local -&gt; local.LocalType  
</pre>
<p><code>lambdaIdent</code> is moderately complicated: it needs to take the name of a function and a list of arguments and determine the correct .NET overload to call. (Even though I'm trying to keep this compiler simple, we need overload resolution in order to call <code>Console.WriteLine</code> -- we can't write hello world without it.)</p>
<p>First, have we ourselves defined a function with the right name?</p>
<pre class="brush: fsharp">
and lambdaIdent args env (a : string) =
    let envMatches = 
        maybe {
            let! v = Map.tryFind a env
            let! r =
                match v with
                | LambdaRef _ -&gt; Some v
                | _ -&gt; None
            return r
        } |&gt; Option.to_list
</pre>
<p>Note: the <code>maybe</code> keyword isn't built into F#; we're using the F# equivalent of Haskell's Maybe monad, which <a href="http://en.wikibooks.org/wiki/F_Sharp_Programming/Computation_Expressions">a few</a> <a href="http://tomasp.net/articles/fsharp-iv-lang.aspx">other</a> <a href="http://codebetter.com/blogs/matthew.podwysocki/archive/2008/10/13/functional-c-linq-as-a-monad.aspx">people</a> have written about. Its purpose is to execute statements until one of them returns <code>None</code>; the result of the <code>maybe</code> block is determined by the <code>return r</code> at the bottom.</p>
<p>At this point, <code>envMatches</code> is a list of one or no <code>LambdaRef</code> nodes, taken from our environment. Next: attempting to parse the method name as <code>Namespace.Class.Method</code>. Again, note the use of <code>maybe</code> to simplify the code that deals with <code>Option</code> variables:</p>
<pre class="brush: fsharp">
    let clrTypeAndMethodName = 
        maybe {
            let! (typeName, methodName) = 
                match a.LastIndexOf('.') with
                | -1 -&gt; None
                | n -&gt; Some (a.Substring(0, n), a.Substring(n + 1))

            let! clrType =
                referencedAssemblies
                |&gt; List.map (fun assembly -&gt; 
                    usingNamespaces 
                    |&gt; List.map (fun usingNamespace -&gt; (assembly, usingNamespace)))
                |&gt; List.concat
                |&gt; List.tryPick (fun (assembly, usingNamespace) -&gt; option_of_nullable &lt;| assembly.GetType(usingNamespace + "." + typeName))

            return (clrType, methodName)
        } 
</pre>
<p><code>referencedAssemblies</code> and <code>usingNamespaces</code> are hard-coded equivalents to C#'s assembly references and <code>using</code> statements. Next: a list of all the .NET methods with the right name, albeit maybe without the right parameter list:</p>
<pre class="brush: fsharp">
    let clrMatches =
        match clrTypeAndMethodName with
        | Some (clrType, methodName) -&gt; 
            clrType.GetMethods(BindingFlags.Public ||| BindingFlags.Static) 
            |&gt; List.of_array
            |&gt; List.filter (fun m -&gt; m.Name = methodName)
            |&gt; List.map makeLambdaRef

        | None -&gt; 
            [ ]
</pre>
<p>A function that determines whether a function's parameter list of compatible with a set of arguments. The <code>isParamArray</code> parameter indicates whether the .NET method has a variable parameter list (such as C#: <code>void WriteLine(string format, params object[] args)</code>).</p>
<pre class="brush: fsharp">
    let argsMatchParameters = function
        | LambdaRef (_, isParamArray, parameterTypes) -&gt;
            let rec argsMatchParameters' argTypes (parameterTypes : #Type list) =
                match argTypes, parameterTypes with
                | [ ], [ ] -&gt; 
                    // No args and no parameters -&gt; always OK
                    true

                | [ ], [ _ ] -&gt; 
                    // No args and one parameter -&gt; OK only for params array methods
                    isParamArray

                | [ ], _ -&gt;
                    // No args and two or more parameters -&gt; never OK
                    false

                | argType :: otherArgTypes, [ parameterType ] when isParamArray -&gt; 
                    // One or more args and one parameter, in a params array method -&gt;
                    //  OK if the types of the first arg and the params array are compatible,
                    //  and the rest of the args match the params array
                    parameterType.GetElementType().IsAssignableFrom(argType) 
                    &amp;&amp; argsMatchParameters' otherArgTypes parameterTypes

                | argType :: otherArgTypes, parameterType :: otherParameterTypes -&gt; 
                    // One or more args and one or more parameters -&gt; 
                    //  OK if the types of the first arg and parameter are compatible, 
                    //  and the rest of the args match the rest of the parameters
                    parameterType.IsAssignableFrom(argType) 
                    &amp;&amp; argsMatchParameters' otherArgTypes otherParameterTypes

                | _ :: _, [ ] -&gt; 
                    // One or more args and no parameters -&gt; never OK
                    false

            argsMatchParameters' (List.map (typeOf env) args) parameterTypes

        | _ -&gt; false
</pre>
<p>Finally, a combined list of all candidates (both from the environment and from .NET), the method overloads whose parameters are compatible with our arguments, and the chosen overload itself. When given more than one compatible overload we pick the first one we've given. (The ECMA C# spec defines detailed rules for picking the most appropriate method overload, but we've ignoring those in our language.)</p>
<pre class="brush: fsharp">
    let candidates = List.append envMatches clrMatches
    match candidates with
    | [ ] -&gt; raise &lt;| Compiler(sprintf "no method called %s" a)
    | _ -&gt; ()

    let allMatches = List.filter argsMatchParameters candidates
    match allMatches with
    | [ ] -&gt; raise &lt;| Compiler(sprintf "no overload of %s is compatible with %A" a args)
    | firstMatch :: _ -&gt; firstMatch
</pre>
<p>We're now able to take a method name (as a <code>string</code>) and a list of arguments (as <code>LispVal</code> nodes), and decide what to call, whether it's one of our own functions or a method in a .NET library. We've done a large chunk of the work ahead of the next post, in which we'll finally get round to generating some useful IL.</p>

